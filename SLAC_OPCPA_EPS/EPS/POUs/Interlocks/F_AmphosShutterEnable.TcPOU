<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="F_AmphosShutterEnable" Id="{a7fc5b7f-48ee-48e8-a6f9-0fc0af7200c2}" SpecialFunc="None">
    <Declaration><![CDATA[// Interlock logic for the Amphos shutter
FUNCTION F_AmphosShutterEnable : BOOL
VAR_INPUT
	bAmphosOn : BOOL; // Whether the amphos is on
	fOpcpaVoltage : LREAL; // The voltage of the OPCPA
	bLoopTempOverride01 : BOOL; // Whether the chiller loop error is overriden
	bLoopTempOverride02 : BOOL; // Whether the chiller loop error is overriden
	bLoopTempOverride03 : BOOL; // Whether the chiller loop error is overriden
	bLoopTempOverride04 : BOOL; // Whether the chiller loop error is overriden
END_VAR
VAR_IN_OUT CONSTANT
	stErrors : ST_ErrorStates; // Currently detected errors
	stOpcpaSetpoints : ST_LaserSetpoints; // OPCPA setpoints
END_VAR
VAR
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF stErrors.bHardwareFailure // Close shutter if there is a hardware failure
	OR stErrors.bLeakOpticalTableAmphosOn OR stErrors.bLeakOpcpaCarbideOn OR stErrors.bLeakUnderTable // Close shutter if there is a leak on the optical table with the amphos on, a leak under the table, or a leak inside the OPCPA with Carbide on
	OR bAmphosOn AND (
		(NOT bLoopTempOverride01 AND stErrors.bChillerLoop01) OR 
		(NOT bLoopTempOverride02 AND stErrors.bChillerLoop02) OR 
		(NOT bLoopTempOverride03 AND stErrors.bChillerLoop03) OR 
		(NOT bLoopTempOverride04 AND stErrors.bChillerLoop04)) // Close shutter if the amphos is on and the temperature is too high in any loop. Ignore if operator override is enabled
	OR fOpcpaVoltage < stOpcpaSetpoints.nMinVoltage AND stErrors.bOpcpaBeam // Close shutter if in OPCPA beam error state and OPCPA voltage too low
	OR stErrors.bTempMon01 OR stErrors.bTempMon02 OR stErrors.bTempMon03 OR stErrors.bTempMon04 OR stErrors.bTempMon05 OR 
		stErrors.bTempMon06 OR stErrors.bTempMon07 OR stErrors.bTempMon08 // Close shutter if temperature too high at any monitor location
	OR stErrors.bDumpChiller // Close shutter if there is a Dump Chiller error
	OR stErrors.bAmphosMRC02 OR stErrors.bAmphosMRC03 // Close shutter if there are errors on the additional MRCs
THEN
	F_AmphosShutterEnable := FALSE;
ELSE
	F_AmphosShutterEnable := TRUE;
END_IF
]]></ST>
    </Implementation>
    <LineIds Name="F_AmphosShutterEnable">
      <LineId Id="593" Count="0" />
      <LineId Id="592" Count="0" />
      <LineId Id="597" Count="4" />
      <LineId Id="603" Count="3" />
      <LineId Id="596" Count="0" />
      <LineId Id="394" Count="0" />
      <LineId Id="594" Count="1" />
      <LineId Id="395" Count="0" />
      <LineId Id="607" Count="0" />
      <LineId Id="7" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>