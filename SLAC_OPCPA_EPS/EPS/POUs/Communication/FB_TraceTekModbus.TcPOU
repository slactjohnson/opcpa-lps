<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="FB_TraceTekModbus" Id="{0f7af4f5-240b-40c0-81d3-4ad14d941ee4}" SpecialFunc="None">
    <Declaration><![CDATA[// Communicates with the TraceTek hardware over Modbus to get more information about a leak
FUNCTION_BLOCK FB_TraceTekModbus
VAR_INPUT
	nModbusSlaveAddress : BYTE; // Modbus address of slave to be read from
	bLeakDetected : BOOL; // Whether a leak has been detected
	bResetError : BOOL; // Reset signal for error
END_VAR
VAR_OUTPUT
	{attribute 'pytmc' := '
		pv: LOC_RES
		io: input
	'}
	nLocationResistance: UINT; // Used for calculating the location of the leak
	{attribute 'pytmc' := '
		pv: ERROR
		io: i
		field: OSV MAJOR
		field: ZSV NO_ALARM
		field: ZNAM OK
		field: ONAM ERROR
	'}
	bError : BOOL := FALSE; // Flag for Modbus communication errors
	nErrorId : MODBUS_ERRORS := MODBUS_ERRORS.MODBUSERROR_NO_ERROR; // Description of Modbus error
END_VAR
VAR
	aModbusBuffer : WORD := 16#FFFF;  // Infinite until real value is read from device
	fbModbusMaster : ModbusRtuMaster_KL6x22B := (
		Quantity := 1,
		MBAddr := 1,
		cbLength := SIZEOF(aModbusBuffer),
		pMemoryAddr := ADR(aModbusBuffer),
		Timeout := T#5S
	);
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bResetError THEN // Reset modbus error
	bError := FALSE;
	nErrorId := MODBUS_ERRORS.MODBUSERROR_NO_ERROR;
END_IF

// Update modbus status
fbModbusMaster.Execute := FALSE;
fbModbusMaster.ReadInputRegs();

IF fbModbusMaster.Error THEN
	bError := TRUE;
	nErrorId := fbModbusMaster.ErrorId;
END_IF

IF NOT fbModbusMaster.BUSY AND bLeakDetected THEN // Start reading
	fbModbusMaster.UnitID := nModbusSlaveAddress;
	fbModbusMaster.Execute := TRUE;
	fbModbusMaster.ReadInputRegs();
END_IF

nLocationResistance := WORD_TO_UINT(aModbusBuffer);]]></ST>
    </Implementation>
    <LineIds Name="FB_TraceTekModbus">
      <LineId Id="115" Count="2" />
      <LineId Id="213" Count="0" />
      <LineId Id="216" Count="0" />
      <LineId Id="214" Count="1" />
      <LineId Id="204" Count="1" />
      <LineId Id="207" Count="0" />
      <LineId Id="209" Count="0" />
      <LineId Id="208" Count="0" />
      <LineId Id="206" Count="0" />
      <LineId Id="124" Count="0" />
      <LineId Id="163" Count="0" />
      <LineId Id="131" Count="2" />
      <LineId Id="119" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>